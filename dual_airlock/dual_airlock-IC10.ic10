alias intVent d0
alias intDoor d1
alias gasSensor d2
alias extVent d3
alias extDoor d4
alias airlock d5
alias intDoorPad r0
alias extDoorPad r1
alias sensorValue r2
alias airLockState r3
alias doorHash r4
define intPressure 98
define extPressure 0
init:
l doorHash intDoor PrefabHash
sb doorHash Mode 1 #sets all the doors at once
sb doorHash Lock 0 #Lock all the doors at once
s intDoor Open 1 #Set internal door open
s extDoor Open 0 #Set external door closed
s intVent On 0 #set internal vent off
s extVent On 0 #Set external vent off
internalOpenLoop: #loop when internal door open
	l intDoorPad intDoor Setting # read if pad is used
	l extDoorPad extDoor Setting # read if pad is used
	l sensorValue gasSensor Pressure # read gas sensor in airlock
	l airLockState airlock Setting #read status of airlock 2
	beq airLockState 0 closeIntDoor # if db on ic for airlock 2 Setting = 0 go to closeIntDoor loop
	beq intDoorPad 0 closeIntDoor # if pad on door Setting = 0 go to closeIntDoor loop
	beq extDoorPad 1 closeIntDoor # if pad on door Setting = 0 go to closeIntDoor loop
	yield # pause a tick
j internalOpenLoop # jump to internalOpenLoop
closeIntDoor: # loop when closing internal door
s db Setting 1 # set db to 1 to tell airlock 2 to also cycle
s intDoor Open 0 # close internal door
emptyLockAir:
s intVent Mode 1 # set intVent to suck air out of airlock
s intVent On 1 # set intVent to on
waitLockAirEmpty: #loop waiting for airlock to empty
	l sensorValue gasSensor Pressure # read sensor
	beq sensorValue 0 fillLockAtmos # if sensor value = 0 go to fillLockAtmos
	yield # pause a tick
j waitLockAirEmpty # jump to waitLockAirEmpty
fillLockAtmos:
s intVent On 0 # turn intVent off
s extVent Mode 0 #set extVent to fill atmosphere into airlock
s extVent On 1 #turn exxtVent on
waitLockAtmosFill: #loop wait to fill airlock to atmos pressure
	l sensorValue gasSensor Pressure #read sensor
	bge sensorValue extPressure openExtDoor #if sensor value >= atmos pressure jump openExtDoor
	yield # pause a tick
j waitLockAtmosFill # jump waitLockAtmosFill
openExtDoor:
s extVent On 0 # turn extVent off
s extDoor Open 1 #open extDoor
externalOpenLoop: #loop when external door is open
	l intDoorPad intDoor Setting #read pad on internal door
	l extDoorPad extDoor Setting #read pad on external door
	l airLockState airlock Setting #read db on other airlock ic
	beq airLockState 1 closeExtDoor # if db on ic for airlock 2 Setting = 0 go to closeIntDoor loop
	beq intDoorPad 1 closeExtDoor # if pad on door Setting = 0 go to closeIntDoor loop
	beq extDoorPad 0 closeExtDoor # if pad on door Setting = 0 go to closeIntDoor loop
	yield #pause a tick
j externalOpenLoop #jump externalOpenLoop 
closeExtDoor:
s db Setting 0 #set db = 0 so other airlock ic knows to cycle
s extDoor Open 0 #close external door
emptyLockAtmos:
s extVent Mode 1 #set extVent to suck atmos out of airlock
s extVent On 1 #turn extVent on
waitLockAtmosEmpty: #loop wait for airlock to empty all atmos
	l sensorValue gasSensor Pressure #read sensor
	beq sensorValue 0 fillLockAir # if sensor value = 0 jump fill air
j waitLockAtmosEmpty # jump waitLockAtmosEmpty
fillLockAir:
s extVent On 0 #turn extVent off
s intVent Mode 0 # set intVent to fill air into airlock
s intVent On 1 # turn intVent on
waitLockAirFill: #loop wait to fill desired pressure of air into airlock
	l sensorValue gasSensor Pressure # read sensor
	bge sensorValue intPressure openIntDoor #if sensor value >= desired pressure jump openIntDoor
j waitLockAirFill # jump waitLockAirFill
openIntDoor:
s intVent On 0 #turn intVent off
s intDoor Open 1 #open internal door
j internalOpenLoop # jump to internal door open loop
